Clever Garnet Jaguar

Medium

# Liquidator can avoid resolving bad debt with dust supply/transfer while seizing all the borrower's collateral

### Summary

Whenever a position with one collateral and many debt reserves is up for liquidation with claiming all the collateral, its debt reserves will be deemed bad debt and cleaned up during `executeLiquidationCall()`, which will bear a significant cost for a liquidator.

To avoid that the liquidator can create an additional collateral for the borrower. For example, can send a dust amount of non-isolated mode aToken not used by them yet, enabling it as a collateral via `executeFinalizeTransfer()`. This can be used by liquidators routinely as profit enhancement and leaves all the bad debt intact with deficit not formed.

### Root Cause

When `vars.totalCollateralInBaseCurrency > vars.collateralToLiquidateInBaseCurrency` in `executeLiquidationCall()` it is `hasNoCollateralLeft == false` and bad debt clean-up is avoided. For a bad debt bearing position with many debt reserves, liquidators will do that as long as this is profitable, which can frequently be the case on L1.

### Internal Pre-conditions

Borrower being liquidated has one collateral, with other supplies being not used as collaterals, and a number of debts. This can be quite common since 3.3 spreading over many debt reserves comes as a natural remedy from 50% rule change from being debt reserve wise to be the whole position wise.

### External Pre-conditions

Gas price is high on liquidations so paying for dust aToken transfer or supply on behalf is cheaper than covering gas costs of the deficit formation for all debt reserves of the borrower.

### Attack Path

The goal is to trigger `setUsingAsCollateral(id, true)` with some additional action that doesn't require borrower participation. This can be supply with `onBehalfOf = borrower` on straightforward aToken transfer, which is cheaper:

`executeFinalizeTransfer()`, [SupplyLogic.sol#L216-L230](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/SupplyLogic.sol#L216-L230):

```
      if (params.balanceToBefore == 0) {
        DataTypes.UserConfigurationMap storage toConfig = usersConfig[params.to];
        if (
          ValidationLogic.validateAutomaticUseAsCollateral(
            reservesData,
            reservesList,
            toConfig,
            reserve.configuration,
            reserve.aTokenAddress
          )
        ) {
>>        toConfig.setUsingAsCollateral(reserveId, true);
          emit ReserveUsedAsCollateralEnabled(params.asset, params.to);
        }
      }
```

### Impact

Since allowing the 50% of all the total debt to be liquidated at once is a extra payoff for liquidators at the expense of the borrowers in order to provide bad debt clearing and deficit formation, the failure to do so is a direct loss for the borrowers from the 3.3 release. I.e. in the described circumstances nothing changes bad debt vice, it's still unrealized and require manual DAO intervention (repaying on behalf), but borrowers now lose more LB to liquidators.

### PoC

According to the contest snapshot it's about `145k` for [aToken transfer](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/snapshots/AToken.transfer.json#L8) with enabling the collateral. [Supply]((https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/snapshots/Pool.Operations.json#L16)) looks to be more expensive, `176k`.

[AToken.transfer.json#L2](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/snapshots/AToken.transfer.json#L2)

```json
  "full amount; receiver: ->enableCollateral": "144881",
```

It's about 100k per [an additional asset](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/snapshots/Pool.Operations.json#L4-L5) bad debt clean-up. Whenever a bad debt bearing borrower have more than 2 debt reserves with one of them capable to take all the collateral it's profitable to transfer aToken and avoid the cleanup (`100*k > 145 if k > 1`, where `k` is number of additional debt reserves).

### Mitigation

Since user can always enable the collateral manually one way to control for the issue is to require that automatic use happens on non-dust amount addition only, e.g.:

[ValidationLogic.sol#L621-L641](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/ValidationLogic.sol#L621-L641)

```diff
  function validateAutomaticUseAsCollateral(
    mapping(address => DataTypes.ReserveData) storage reservesData,
    mapping(uint256 => address) storage reservesList,
    DataTypes.UserConfigurationMap storage userConfig,
    DataTypes.ReserveConfigurationMap memory reserveConfig,
+   uint256 amountInBaseCurrency,
    address aTokenAddress
  ) internal view returns (bool) {
    if (reserveConfig.getDebtCeiling() != 0) {
      // ensures only the ISOLATED_COLLATERAL_SUPPLIER_ROLE can enable collateral as side-effect of an action
      IPoolAddressesProvider addressesProvider = IncentivizedERC20(aTokenAddress)
        .POOL()
        .ADDRESSES_PROVIDER();
      if (
        !IAccessControl(addressesProvider.getACLManager()).hasRole(
          ISOLATED_COLLATERAL_SUPPLIER_ROLE,
          msg.sender
        )
      ) return false;
-   }
+   } else {
+       // ensures that amount that triggered the action is not below minimum
+       if (amountInBaseCurrency < LiquidationLogic.MIN_LEFTOVER_BASE) return false;
+   }   
    return validateUseAsCollateral(reservesData, reservesList, userConfig, reserveConfig);
  }
```

All the uses of `validateAutomaticUseAsCollateral` will need to supply the base currency equivalent amount for the action, e.g.:

[SupplyLogic.sol#L215-L230](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/SupplyLogic.sol#L215-L230)

```diff
      if (params.balanceToBefore == 0) {
        DataTypes.UserConfigurationMap storage toConfig = usersConfig[params.to];
+       uint256 assetPrice = IPriceOracleGetter(params.oracle).getAssetPrice(params.asset);
+       uint256 assetUnit = 10 ** reserve.configuration.getDecimals();
+       uint256 amountInBaseCurrency = (params.amount * assetPrice) / assetUnit;
        if (
          ValidationLogic.validateAutomaticUseAsCollateral(
            reservesData,
            reservesList,
            toConfig,
            reserve.configuration,
+           amountInBaseCurrency,
            reserve.aTokenAddress
          )
        ) {
          toConfig.setUsingAsCollateral(reserveId, true);
          emit ReserveUsedAsCollateralEnabled(params.asset, params.to);
        }
      }
```
