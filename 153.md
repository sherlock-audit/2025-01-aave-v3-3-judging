Able Jade Bear

Medium

# Borrow cap does not take deficit into account.


### Summary

In Aave, for each reserve, there is a definition of [borrow cap](https://aave.com/docs/primitives/reserve). The borrow cap is used to limit the total amount of a token that can be supplied and borrowed from a reserve. According to the docs, the caps are used for:

> These caps are crucial for maintaining liquidity and preventing overexposure during volatile market conditions.

The bug here is, the borrow cap does not take reserve deficit into account, so it is possible that the asset is exposed to larger risks than expected.

### Root Cause

In `ValidationLogic.sol`, when validating a borrow action, the reserve deficit is not taken into account.

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/ValidationLogic.sol#L174-L190

```solidity
    vars.reserveDecimals = params.reserveCache.reserveConfiguration.getDecimals();
    vars.borrowCap = params.reserveCache.reserveConfiguration.getBorrowCap();
    unchecked {
      vars.assetUnit = 10 ** vars.reserveDecimals;
    }

    if (vars.borrowCap != 0) {
      vars.totalSupplyVariableDebt = params.reserveCache.currScaledVariableDebt.rayMul(
        params.reserveCache.nextVariableBorrowIndex
      );

      vars.totalDebt = vars.totalSupplyVariableDebt + params.amount;

      unchecked {
@>      require(vars.totalDebt <= vars.borrowCap * vars.assetUnit, Errors.BORROW_CAP_EXCEEDED);
      }
    }
```

A possible scenario is:

1. Borrow cap is 10000 USD
2. User creates a position with 9950 USD debt.
3. User is liquidated, and reserve deficit is 9950 USD. However, variableDebtToken supply is cleared to 0.
4. Another user come and opens another position with 9950 USD.

Now, even though the borrow cap is 10000 USD, the reserve is facing a total debt of 9950+9950 = 19900 USD. This breaks the invariant of a borrow cap, and exposes the reserve to larger risks.

### Internal pre-conditions

1. Due to bad debt liquidation, a certain reserve asset has non-zero deficit that is not yet repayed by the Umbrella.
2. Users taking out borrows and make the total debt exceed the borrow cap.

Note that the Umbrella does NOT have to immediately clear the deficit, as written here: https://github.com/aave-dao/aave-v3-origin/blob/094e6ec662703dec3ce5f9749527a01dbcbc1c97/docs/3.3/Aave-v3.3-features.md.

> eliminateReserveDeficit assumes for umbrella to have the tokens to be burned. In case of assets having virtual accounting enabled, aTokens will be burned. In case of virtual accounting being disabled, the underlying will be disposed of. Depending on the coverage asset and reserve configuration(e.g. if coverage is done via underlying, and caps don't allow depositing) it might be that it us not possible to receive the aToken. This is expected and considered a non-issue as the elimination of the deficit has no strict time constraint.


### External pre-conditions

None required.

### Attack Path

None required.

### Impact

Total debt amount of a token can exceed borrow cap, and expose the reserve to larger risks.

### PoC

Add the following code to `Pool.Liquidations.t.sol`. What it does is the following:

1. Alice creates a position of collateral=0.5e8 WBTC, debt=11000e6 USDX. Initially, WBTC price = 27000e8, USDX price = 1e8. USDX has a borrow cap of 11000 USD.
2. Alice tries to borrow another 1e6 USDX but fails due to borrow cap.
3. WBTC price drops 40% to 16200e8, and the position can be liquidated.
4. Fully liquidate position. Since collateral is zero, bad debt turns to deficit. The amount of deficit is ~3285e6 USDX.
5. Alice tries to open another position with 11000e6 USDX, and still succeed. The total debt is now 3285+11000, which exceeds the borrow cap of 11000 USD.


```solidity

  function test_poc_borrow_cap() public {
    uint256 borrowAmount = 11000e6;
    vm.startPrank(poolAdmin);
    contracts.poolConfiguratorProxy.setBorrowCap(tokenList.usdx, 11000);
    vm.stopPrank();

    // Step 1: Collateral = 0.5e8 WBTC, Debt = 11000e6 USDX
    vm.startPrank(alice);
    contracts.poolProxy.supply(tokenList.wbtc, 0.5e8, alice, 0);
    contracts.poolProxy.setUserUseReserveAsCollateral(tokenList.wbtc, true);
    contracts.poolProxy.borrow(tokenList.usdx, borrowAmount, 2, 0, alice);
    vm.stopPrank();

    // Step 2: Try to borrow another 1e6 USDX but fails due to borrow cap.
    vm.startPrank(alice);
    vm.expectRevert(bytes(Errors.BORROW_CAP_EXCEEDED));
    contracts.poolProxy.borrow(tokenList.usdx, 1e6, 2, 0, alice);
    vm.stopPrank();

    console.log("> Initial price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));

    // Step 3: WBTC Price drops by 40%. 27000 -> 16200.
    LiquidationInput memory params = _loadLiquidationInput(
      alice,
      tokenList.wbtc,
      tokenList.usdx,
      UINT256_MAX,
      tokenList.wbtc,
      40_00
    );

    console.log("> Current price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));

    (, , address varDebtToken) = contracts.protocolDataProvider.getReserveTokensAddresses(
      params.debtAsset
    );
    uint256 userDebtBefore = IERC20(varDebtToken).balanceOf(params.user);
    uint256 liquidatorBalanceBefore;
    if (params.receiveAToken) {
      (address atoken, , ) = contracts.protocolDataProvider.getReserveTokensAddresses(
        params.collateralAsset
      );
      liquidatorBalanceBefore = IERC20(atoken).balanceOf(bob);
    } else {
      liquidatorBalanceBefore = IERC20(params.collateralAsset).balanceOf(bob);
    }

    // Step 4: Output logs before liquidation.
    {
      console.log("> Before liquidation:");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
    }

    // Step 5: Liquidate.
    vm.prank(bob);
    contracts.poolProxy.liquidationCall(
      params.collateralAsset,
      params.debtAsset,
      params.user,
      params.liquidationAmountInput,
      params.receiveAToken
    );

    _afterLiquidationChecksVariable(params, bob, liquidatorBalanceBefore, userDebtBefore);

    // Step 6: Output logs after liquidation.
    {
      console.log("> After liquidation");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
      console.log("USDX Deficit: ", contracts.poolProxy.getReserveDeficit(tokenList.usdx));

    }

    // Step 7: Alice tries to borrow 11000e6 USDX again, and succeeds.
    vm.startPrank(alice);
    contracts.poolProxy.supply(tokenList.wbtc, 1e8, alice, 0);
    contracts.poolProxy.borrow(tokenList.usdx, borrowAmount, 2, 0, alice);
    vm.stopPrank();
  }
```

Console output is the following. We can see during the end, with a deficit of 3285e6 USDX, Alice is still able to borrow 11000e6 USDX even though the borrow cap was 11000. The means the total debt exposure exceeded the borrow cap.

```bash
Ran 1 test for tests/protocol/pool/Pool.Liquidations.t.sol:PoolLiquidationTests
[PASS] test_poc_borrow_cap() (gas: 1054433)
Logs:
  > Initial price:
  wbtc price = 2700000000000
  usdx price = 100000000
  > Current price:
  wbtc price = 1620000000000
  usdx price = 100000000
  > Before liquidation:
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 810000000000 1100000000000 633272727272727273
  > After liquidation
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 0 0 115792089237316195423570985008687907853269984665640564039457584007913129639935
  USDX Deficit:  3285714286
```

### Mitigation

Always consider `reserve.deficit` when calculating borrow caps.