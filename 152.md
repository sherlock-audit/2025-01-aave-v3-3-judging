Able Jade Bear

Medium

# Liquidation does not correctly decrease `reserveData[].isolationModeTotalDebt` if the collateral is entirely seized


### Summary

During liquidation events, if user's collateral is completely seized, and the collateral is an isolation mode asset, the `reserveData[].isolationModeTotalDebt` of the collateral is not properly updated. This would effectively lower the debtCeilings of isolation mode assets, because there is unexisting debt that take up `reserveData[].isolationModeTotalDebt`.

### Root Cause

When a position is fully liquidated, if the position has only 1 collateral asset, and the collateral is an isolation mode asset, the `reserveData[].isolationModeTotalDebt` of this asset should decrease by the amount of debt repayed.

For example, if WBTC is the isolation mode collateral asset, and USDC is the borrowed asset. During liquidation, if 1500 USDC is repayed, `reserveData[USDC].isolationModeTotalDebt` should decrease by 150000 (there is an additional 2 decimal precison). This is because the debt for this collateral is effectively being repayed.

The issue here is, if during the liquidation, all of the collateral is seized, the `reserveData[].isolationModeTotalDebt` would not change. The root cause is `collateralReserve.id` would be first removed from the `userConfigBitmap`, which is effectively moving out of isolation mode. Then, when we try to update `reserveData[].isolationModeTotalDebt` in `IsolationModeLogic`, it will not work, because user is not in isolation mode anymore.

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L349-L379
```solidity
    // If the collateral being liquidated is equal to the user balance,
    // we set the currency as not being used as collateral anymore
    if (
      vars.actualCollateralToLiquidate + vars.liquidationProtocolFeeAmount ==
      vars.userCollateralBalance
    ) {
      // @audit-note: This is set to false first.
@>    userConfig.setUsingAsCollateral(collateralReserve.id, false);
      emit ReserveUsedAsCollateralDisabled(params.collateralAsset, params.user);
    }
    ...

    // IsolationModeTotalDebt only discounts `actualDebtToLiquidate`, not the fully burned amount in case of deficit creation.
    // This is by design as otherwise debt debt ceiling would render ineffective if a collateral asset faces bad debt events.
    // The governance can decide the raise the ceiling to discount manifested deficit.

    // @audit-note: Then isolation mode logic is updated.
@>  IsolationModeLogic.updateIsolatedDebtIfIsolated(
      reservesData,
      reservesList,
      userConfig,
      vars.debtReserveCache,
      vars.actualDebtToLiquidate
    );
```

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/IsolationModeLogic.sol#L37-L38
```solidity

  function updateIsolatedDebtIfIsolated(
    mapping(address => DataTypes.ReserveData) storage reservesData,
    mapping(uint256 => address) storage reservesList,
    DataTypes.UserConfigurationMap storage userConfig,
    DataTypes.ReserveCache memory reserveCache,
    uint256 repayAmount
  ) internal {
  	// @audit-note: User may no longer be in isolation mode, because he was removed earlier.
@>  (bool isolationModeActive, address isolationModeCollateralAddress, ) = userConfig
      .getIsolationModeState(reservesData, reservesList);

    if (isolationModeActive) {
      uint128 isolationModeTotalDebt = reservesData[isolationModeCollateralAddress]
        .isolationModeTotalDebt;

      uint128 isolatedDebtRepaid = (repayAmount /
        10 **
          (reserveCache.reserveConfiguration.getDecimals() -
            ReserveConfiguration.DEBT_CEILING_DECIMALS)).toUint128();

      // since the debt ceiling does not take into account the interest accrued, it might happen that amount
      // repaid > debt in isolation mode
      if (isolationModeTotalDebt <= isolatedDebtRepaid) {
        reservesData[isolationModeCollateralAddress].isolationModeTotalDebt = 0;
        emit IsolationModeTotalDebtUpdated(isolationModeCollateralAddress, 0);
      } else {
        uint256 nextIsolationModeTotalDebt = reservesData[isolationModeCollateralAddress]
          .isolationModeTotalDebt = isolationModeTotalDebt - isolatedDebtRepaid;
        emit IsolationModeTotalDebtUpdated(
          isolationModeCollateralAddress,
          nextIsolationModeTotalDebt
        );
      }
    }
  }
```

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/configuration/UserConfiguration.sol#L180
```solidity
  function getIsolationModeState(
    DataTypes.UserConfigurationMap memory self,
    mapping(address => DataTypes.ReserveData) storage reservesData,
    mapping(uint256 => address) storage reservesList
  ) internal view returns (bool, address, uint256) {
@>  if (isUsingAsCollateralOne(self)) {
      uint256 assetId = _getFirstAssetIdByMask(self, COLLATERAL_MASK);

      address assetAddress = reservesList[assetId];
      uint256 ceiling = reservesData[assetAddress].configuration.getDebtCeiling();
      if (ceiling != 0) {
        return (true, assetAddress, ceiling);
      }
    }
    return (false, address(0), 0);
  }
```

### Internal pre-conditions

1. Users has an isolation mode position with single asset as collateral.
2. Position is 100% liquidatable.

### External pre-conditions

1. Liquidators fully liquidate the position.

### Attack Path

No special attack path.

### Impact

This would effectively lower the debtCeilings of isolation mode assets, because there is unexisting debt that take up `reserveData[].isolationModeTotalDebt`.

For example, if debtCeiling was 15000 USD, due to this bug, `reserveData[].isolationModeTotalDebt` may be 15000 USD while there is actually no existing debt. This would prevent normal users from borrowing with the isolation mode collateral.

### PoC

Add the following code to `Pool.Liquidations.t.sol`. What it does is the following:

1. Create a position of collateral=0.5 wbtc, debt=11000e6 usdx. Initially, wbtc price = 27000e8, usdx price = 1e8. wbtc has a debt ceiling of 12000 USD, and currently the `isolationModeTotalDebt` for wbtc is 11000 USD.
2. wbtc price drops 40% to 16200, and the position can be liquidated.
3. Fully liquidate position. Since collateral is zero, bad debt turns to deficit, so total collateral and total debt both turn to zero.
4. User no longer has wbtc as collateral, but wbtc's `isolationModeTotalDebt` is still 11000 USD.

```solidity
  function test_poc_isolation_mode() public {
    uint256 borrowAmount = 11000e6;
    vm.startPrank(poolAdmin);
    contracts.poolConfiguratorProxy.setDebtCeiling(tokenList.wbtc, 12_000_00);
    contracts.poolConfiguratorProxy.setBorrowableInIsolation(tokenList.usdx, true);
    vm.stopPrank();

    // Step 1: Collateral = 0.5e8 WBTC, Debt = 11000e6 USDX
    vm.startPrank(alice);
    contracts.poolProxy.supply(tokenList.wbtc, 0.5e8, alice, 0);
    contracts.poolProxy.setUserUseReserveAsCollateral(tokenList.wbtc, true);
    contracts.poolProxy.borrow(tokenList.usdx, borrowAmount, 2, 0, alice);
    vm.stopPrank();

    console.log("> Initial price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));

    // Step 2: WBTC Price drops by 40%. 27000 -> 16200.
    LiquidationInput memory params = _loadLiquidationInput(
      alice,
      tokenList.wbtc,
      tokenList.usdx,
      UINT256_MAX,
      tokenList.wbtc,
      40_00
    );

    // Step 3: Output logs before liquidation.
    console.log("> Current price:");
    console.log("wbtc price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.wbtc));
    console.log("usdx price =", IAaveOracle(report.aaveOracle).getAssetPrice(tokenList.usdx));
    console.log("isolationModeTotalDebt =", contracts.poolProxy.getReserveData(tokenList.wbtc).isolationModeTotalDebt);

    (, , address varDebtToken) = contracts.protocolDataProvider.getReserveTokensAddresses(
      params.debtAsset
    );
    uint256 userDebtBefore = IERC20(varDebtToken).balanceOf(params.user);
    uint256 liquidatorBalanceBefore;
    if (params.receiveAToken) {
      (address atoken, , ) = contracts.protocolDataProvider.getReserveTokensAddresses(
        params.collateralAsset
      );
      liquidatorBalanceBefore = IERC20(atoken).balanceOf(bob);
    } else {
      liquidatorBalanceBefore = IERC20(params.collateralAsset).balanceOf(bob);
    }

    {
      console.log("> Before liquidation:");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
      console.log("isolationModeTotalDebt =", contracts.poolProxy.getReserveData(tokenList.wbtc).isolationModeTotalDebt);
    }

    // Step 4: Liquidate.
    vm.prank(bob);
    contracts.poolProxy.liquidationCall(
      params.collateralAsset,
      params.debtAsset,
      params.user,
      params.liquidationAmountInput,
      params.receiveAToken
    );

    // Step 5: Output logs after liquidation: `isolationModeTotalDebt` is unchanged.
    _afterLiquidationChecksVariable(params, bob, liquidatorBalanceBefore, userDebtBefore);
    {
      console.log("> After liquidation");
      (
        uint totalCollateralInBaseCurrency,
        uint totalDebtInBaseCurrency,
        ,
        ,
        ,
        uint healthFactor
      ) = contracts.poolProxy.getUserAccountData(params.user);
      console.log("totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor =", totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor);
      console.log("isolationModeTotalDebt =", contracts.poolProxy.getReserveData(tokenList.wbtc).isolationModeTotalDebt);
    }
  }
```

The output of the test is:

```bash
Ran 1 test for tests/protocol/pool/Pool.Liquidations.t.sol:PoolLiquidationTests
[PASS] test_poc_isolation_mode() (gas: 955167)
Logs:
  > Initial price:
  wbtc price = 2700000000000
  usdx price = 100000000
  > Current price:
  wbtc price = 1620000000000
  usdx price = 100000000
  isolationModeTotalDebt = 1100000
  > Before liquidation:
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 810000000000 1100000000000 633272727272727273
  isolationModeTotalDebt = 1100000
  > After liquidation
  totalCollateralInBaseCurrency, totalDebtInBaseCurrency, healthFactor = 0 0 115792089237316195423570985008687907853269984665640564039457584007913129639935
  isolationModeTotalDebt = 1100000
```

Note that even though a part of the debt becomes deficit, and is not deducted from `reserveData[].isolationModeTotalDebt`. However, according to the code comment (seen below), the debt that is repayed should at least be deducted. In our PoC, we can see even the repayed part is not deducted.

https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L370-L372

```solidity
@>  // IsolationModeTotalDebt only discounts `actualDebtToLiquidate`, not the fully burned amount in case of deficit creation.
    // This is by design as otherwise debt debt ceiling would render ineffective if a collateral asset faces bad debt events.
    // The governance can decide the raise the ceiling to discount manifested deficit.
    IsolationModeLogic.updateIsolatedDebtIfIsolated(
      reservesData,
      reservesList,
      userConfig,
      vars.debtReserveCache,
      vars.actualDebtToLiquidate
    );
```

### Mitigation

Move the line `userConfig.setUsingAsCollateral(collateralReserve.id, false);` after isolation mode debt update.