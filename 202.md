Clever Garnet Jaguar

Medium

# Deficit is ignored as if it wasn't ever borrowed in IRM supply-demand logic, so interest rates will be shifted downwards over time

### Summary

`deficit` part of the borrowed funds are now ignored in IRM logic as total debt figure supplied there does not include deficit. This twists any supply-demand logic IRM have / will have as it should't depend on the quality of the debt, but on the what was supplied to the pool and what was loaned from it only. Now deficit is treated as if it was never loaned, which is not correct.

IRM interest rate logic is now twisted proportionally to the realized deficit value compared to the remaining active debt. This doesn't depend on the IRM logic itself as deficit is ignored on ReserveLogic call level.

### Root Cause


`deficit` is not the same as `unbacked` in a way that there was no underlying supply for `unbacked` yet (first goes `mintUnbacked`, then `backUnbacked`), while there was supply for `deficit` (funds were supplied, loaned, then their accrual stopped when the debt was marked as bad, then there will be a coverage supply in `eliminateReserveDeficit()`). I.e. for interest rate logic `were never supplied` and `were supplied, were lost and to be compensated` are not the same situations.

Essentially this is ReserveLogic issue, since any IRM logic has no chance to treat deficit correctly since it's not given to it as a parameter. Unlike `supplyUsageRatio` situation mixing Bridge initiated `unbacked` with `deficit` makes little sense for `borrowUsageRatio`, i.e. estimation of supply-demand situation of the pool, so it can't be calculated in any IRM logic.

In order words, `totalDebt` in IRM logic should include deficit, but can't as it's not provided by ReserveLogic's `updateInterestRatesAndVirtualBalance()`:

[ReserveLogic.sol#L173-L177](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/ReserveLogic.sol#L173-L177)

```solidity
    (uint256 nextLiquidityRate, uint256 nextVariableRate) = IReserveInterestRateStrategy(
      reserve.interestRateStrategyAddress
    ).calculateInterestRates(
        DataTypes.CalculateInterestRatesParams({
>>        unbacked: reserve.unbacked + reserve.deficit,
```

[DefaultReserveInterestRateStrategyV2.sol#L141-L152](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/misc/DefaultReserveInterestRateStrategyV2.sol#L141-L152)

```solidity
    if (params.totalDebt != 0) {
      vars.availableLiquidity =
        params.virtualUnderlyingBalance +
        params.liquidityAdded -
        params.liquidityTaken;

      vars.availableLiquidityPlusDebt = vars.availableLiquidity + params.totalDebt;
>>    vars.borrowUsageRatio = params.totalDebt.rayDiv(vars.availableLiquidityPlusDebt);
      vars.supplyUsageRatio = params.totalDebt.rayDiv(
        vars.availableLiquidityPlusDebt + params.unbacked
      );
    } else {
```

For the protocol accounting viewpoint deficit is still borrowed from the reserve since the borrowing was done for it, while repayment (in any form) wasn't. I.e. it is being `borrowed` similarly to the healthy debt, the difference is that deficit is expected to be repaid by Umbrella (written off), while healthy debt is expected to be repaid by the borrower. Until the repayment is done both types represent active debt, i.e. [what was ever borrowed](https://aave.com/docs/developers/smart-contracts/interest-rate-strategy#view-methods-calculateinterestrates):

```md
    totalDebt
    uint256
>>  The total borrowed from the reserve
```

That is, with the introduction of the deficit the [inclusion of deficit to unbacked](https://github.com/aave-dao/aave-v3-origin/pull/87/files#diff-58500f59184d0c3101d1eb616e016f22027b1b7ca3badb1e8ffaa3fd6739d189L176-R177) done as Certora#M-01 mitigation doesn't look to be enough as, keeping the same variables as in issue description, `totDEBT` is no longer `the amount of money that was borrowed` since deficit was borrowed and wasn't yet repaid, so is still borrowed, just being marked for a write off. I.e. for pool it's still a debt, but frozen and not yield bearing.

It cannot be deemed as if it is written off already because usage ratios and IRM logic is based on the balances of funds, and the corresponding supply side balance is present until aToken burn, i.e. supply reduction, on `executeEliminateDeficit()`:

[LiquidationLogic.sol#L135-L158](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/LiquidationLogic.sol#L135-L158)

```solidity
>>    IAToken(reserveCache.aTokenAddress).burn(
        msg.sender,
        reserveCache.aTokenAddress,
        balanceWriteOff,
        reserveCache.nextLiquidityIndex
      );
    } else {
      // This is a special case to allow mintable assets (ex. GHO), which by definition cannot be supplied
      // and thus do not use virtual underlying balances.
      // In that case, the procedure is 1) sending the underlying asset to the aToken and
      // 2) trigger the handleRepayment() for the aToken to dispose of those assets
      IERC20(params.asset).safeTransferFrom(
        msg.sender,
        reserveCache.aTokenAddress,
        balanceWriteOff
      );
      // it is assumed that handleRepayment does not touch the variable debt balance
>>    IAToken(reserveCache.aTokenAddress).handleRepayment(
        msg.sender,
        // In the context of GHO it's only relevant that the address has no debt.
        // Passing the pool is fitting as it's handling the repayment on behalf of the protocol.
        address(this),
        balanceWriteOff
      );
```

[GhoAToken.sol#L160-L173](https://github.com/aave/gho-core/blob/main/src/contracts/facilitators/aave/tokens/GhoAToken.sol#L160-L173)

```solidity
  /// @inheritdoc IAToken
  function handleRepayment(
    address user,
    address onBehalfOf,
    uint256 amount
  ) external virtual override onlyPool {
    uint256 balanceFromInterest = _ghoVariableDebtToken.getBalanceFromInterest(onBehalfOf);
    if (amount <= balanceFromInterest) {
      _ghoVariableDebtToken.decreaseBalanceFromInterest(onBehalfOf, amount);
    } else {
      _ghoVariableDebtToken.decreaseBalanceFromInterest(onBehalfOf, balanceFromInterest);
>>    IGhoToken(_underlyingAsset).burn(amount - balanceFromInterest);
    }
  }
```

### Internal Pre-conditions

Material deficit was formed for a debt reserve compared to the active debt.

### External Pre-conditions

None, it's internal accounting.

### Attack Path

The impact will be accrued automatically along with deficit formation.

### Impact

IRM logic (any version of it) is artificially shifted towards low usage situation and lower interest rates, despite user funds might being fully/almost fully utilized.

### PoC

As an example, let's suppose there is `900 USDC` of deficit, `100 USDC` of the healthy debt and `100 USDC` of the available liquidity in the pool, no unbacked.

It will be:

* `borrowUsageRatio = 100 / 200  = 0.500` in 3.3 and `1000 / 1100 = 0.909` in 3.2 (1)
* `supplyUsageRatio = 100 / 1100 = 0.091` in 3.3 and `1000 / 1100 = 0.909` in 3.2 (2)

[DefaultReserveInterestRateStrategyV2.sol#L141-L152](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/misc/DefaultReserveInterestRateStrategyV2.sol#L141-L152)

```solidity
    if (params.totalDebt != 0) {
      vars.availableLiquidity =
        params.virtualUnderlyingBalance +
        params.liquidityAdded -
        params.liquidityTaken;

      vars.availableLiquidityPlusDebt = vars.availableLiquidity + params.totalDebt;
>>    vars.borrowUsageRatio = params.totalDebt.rayDiv(vars.availableLiquidityPlusDebt);
>>    vars.supplyUsageRatio = params.totalDebt.rayDiv(
        vars.availableLiquidityPlusDebt + params.unbacked
      );
    } else {
```

`supplyUsageRatio` is a coefficient between yield generating debt, paying `currentVariableBorrowRate`, and yield requiring liquidity, receiving `currentLiquidityRate`:

[DefaultReserveInterestRateStrategyV2.sol#L171-L174](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/misc/DefaultReserveInterestRateStrategyV2.sol#L171-L174)

```solidity
>>  vars.currentLiquidityRate = vars
>>    .currentVariableBorrowRate
>>    .rayMul(vars.supplyUsageRatio)
      .percentMul(PercentageMath.PERCENTAGE_FACTOR - params.reserveFactor);
```

This way 3.3 version of (2), `supplyUsageRatio = 100 / 1100 = 0.091`, looks correct as it is `100` units generate yield, while `1100` units expecting it.

On the other hand, `borrowUsageRatio` should push interest rate higher when the demand is high vs `optimalUsageRatio` and vice versa. So, 3.3 version of (1), `borrowUsageRatio = 100 / 200  = 0.500` is not a correct representation of the supply-demand situation in the pool: liquidity supply is `1100`, this is what was supplied and accrued so far, while the loan demand is `1000`, this is what was taken out as debt and accrued as its interest, i.e. the 3.2 version of (1), `1000 / 1100 = 0.909`, is valid instead.

In other words it is high demand, very low coverage situation: almost all the tokens supplied are utilized by loans (`1000` of `1100` are taken), which have low coverage for the supply (`100` loans generate interest for `1100` supply, because of the bad debt, represented by deficit). By `borrowUsageRatio` computed it is low demand, very low coverage instead, which doesn't represent the real situation and misalign LP incentives. That is, the deficit can't be ignored for borrow demand calculation as demand-supply is a function of loan origination, which happens before debt quality is realized. When debt is issued the demand is set, if supply is fixed for the sake of the example, and the demand doesn't change if that debt turned out to be bad, as the loan is originated already and that is not replayed when loan is Â§written off.

Current situation can be equivalent to shifting `optimalUsageRatio` and can even surpass that.

Rate dynamics depend on `excessBorrowUsageRatio = (borrowUsageRatio - optimalUsageRatio) / (1 - optimalUsageRatio) = 1 - (1 - borrowUsageRatio) / (1 - optimalUsageRatio)` when `borrowUsageRatio > optimalUsageRatio`, and on `borrowUsageRatio / optimalUsageRatio` when `borrowUsageRatio <= optimalUsageRatio`.

Continuing the same example (1), `borrowUsageRatio = 100 / 200 = 0.500` now and `1000 / 1100 = 0.909` before ignoring the deficit, if `optimalUsageRatio = 0.8` it's `borrowUsageRatio > optimalUsageRatio` and `excessBorrowUsageRatio = (1 - 0.909) / (1 - 0.8) = 0.45`. No `optimalUsageRatio` can make it equivalent when `borrowUsageRatio == 0.5` since even when `optimalUsageRatio == 0` it's `excessBorrowUsageRatio = (1 - 0.5) / (1 - 0) = 0.5 > 0.45`.

### Mitigation


Consider including the deficit into both sides of the `borrowUsageRatio` fraction, e.g.:

[DefaultReserveInterestRateStrategyV2.sol#L141-L154](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/misc/DefaultReserveInterestRateStrategyV2.sol#L141-L154)

```solidity
    if (params.totalDebt != 0) {
      vars.availableLiquidity =
        params.virtualUnderlyingBalance +
        params.liquidityAdded -
        params.liquidityTaken;

      vars.availableLiquidityPlusDebt = vars.availableLiquidity + params.totalDebt;
-     vars.borrowUsageRatio = params.totalDebt.rayDiv(vars.availableLiquidityPlusDebt);
+     vars.borrowUsageRatio = (params.totalDebt + params.deficit).rayDiv(vars.availableLiquidityPlusDebt + params.deficit);
      vars.supplyUsageRatio = params.totalDebt.rayDiv(
-       vars.availableLiquidityPlusDebt + params.unbacked
+       vars.availableLiquidityPlusDebt + params.unbacked + params.deficit
      );
    } else {
      return (0, vars.currentVariableBorrowRate);
    }
```

As a side effect there will no longer be a jump in `borrowUsageRatio` when deficit is increased on bad debt liquidations. Also new bad debt deficit will be treated similarly to the bad debt positions existing pre 3.3 release.

Since it requires adding a `deficit` variable there is no need to include it in `unbacked`:

[ReserveLogic.sol#L173-L186](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/logic/ReserveLogic.sol#L173-L186)

```diff
    (uint256 nextLiquidityRate, uint256 nextVariableRate) = IReserveInterestRateStrategy(
      reserve.interestRateStrategyAddress
    ).calculateInterestRates(
        DataTypes.CalculateInterestRatesParams({
-         unbacked: reserve.unbacked + reserve.deficit,
+         unbacked: reserve.unbacked,
+         deficit: reserve.deficit,
          liquidityAdded: liquidityAdded,
          liquidityTaken: liquidityTaken,
          totalDebt: totalVariableDebt,
          reserveFactor: reserveCache.reserveFactor,
          reserve: reserveAddress,
          usingVirtualBalance: reserveCache.reserveConfiguration.getIsVirtualAccActive(),
          virtualUnderlyingBalance: reserve.virtualUnderlyingBalance
        })
      );
```

[DataTypes.sol#L311-L320](https://github.com/sherlock-audit/2025-01-aave-v3-3/blob/main/aave-v3-origin/src/contracts/protocol/libraries/types/DataTypes.sol#L311-L320)

```diff
  struct CalculateInterestRatesParams {
    uint256 unbacked;
+   uint256 deficit;
    uint256 liquidityAdded;
    uint256 liquidityTaken;
    uint256 totalDebt;
    uint256 reserveFactor;
    address reserve;
    bool usingVirtualBalance;
    uint256 virtualUnderlyingBalance;
  }
```